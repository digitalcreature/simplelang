@@grammar::SimpleLang

start
	= { expression } * $
	;

expression
	= assign
	| sum
	;

assign::Assign
	= left:sum op:'=' ~ right:expression
	;

sum
	= add
	| sub
	| factor
	;

add::Add
	= left:factor op:'+' ~ right:sum
	;

sub::Subtract
	= left:factor op:'-' ~ right:sum
	;

factor
	= mult
	| div
	| atom
	;

mult::Multiply
	= left:atom op:'*' ~ right:factor
	;

div::Divide
	= left:atom op:'/' ~ right:factor
	;

atom
	= '(' @:expression ')'
	| number
	| identifier
	;

number::Number
	= value:/[+-]?([0-9]*\.)?[0-9]+/
	;

identifier::Identifier
	= id:/[a-zA-Z_@$#%][a-zA-Z0-9_@$#%]*/
	;
